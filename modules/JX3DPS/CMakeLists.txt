cmake_minimum_required(VERSION 3.5)

project(JX3DPS VERSION ${PROJECT_VERSION} LANGUAGES CXX)

set(CMAKE_INCLUDE_CURRENT_DIR ON)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(DYLIB_CURRENT_VERSION ${PROJECT_VERSION})

add_definitions(-DEXPORT_JX3DPS)

file(GLOB_RECURSE HEADER_FILES ${CMAKE_CURRENT_SOURCE_DIR}/*.h*)
file(GLOB_RECURSE SOURCE_FILES ${CMAKE_CURRENT_SOURCE_DIR}/*.c*)

add_library(${PROJECT_NAME} ${BUILD_LIBS_TYPE}
    ${HEADER_FILES}
    ${SOURCE_FILES}
)

target_link_libraries(${PROJECT_NAME} PRIVATE)

target_include_directories(
    ${PROJECT_NAME} PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
    $<INSTALL_INTERFACE:include>
)
set(HEADERS ${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}.h)
set_target_properties(${PROJECT_NAME} PROPERTIES
    PUBLIC_HEADER "${HEADERS}"
    VERSION ${PROJECT_VERSION}
    SOVERSION ${PROJECT_VERSION_MAJOR}
)

install(TARGETS ${PROJECT_NAME}
    EXPORT ${PROJECT_NAME}Targets
    LIBRARY DESTINATION ${PROJECT_NAME}/lib
    ARCHIVE DESTINATION ${PROJECT_NAME}/lib
    RUNTIME DESTINATION ${PROJECT_NAME}/bin
    PUBLIC_HEADER DESTINATION ${PROJECT_NAME}/include
)

include(CMakePackageConfigHelpers)
write_basic_package_version_file(
    ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake
    VERSION ${PACKAGE_VERSION}
    COMPATIBILITY AnyNewerVersion
)

install(EXPORT ${PROJECT_NAME}Targets
    FILE ${PROJECT_NAME}Targets.cmake
    #NAMESPACE JX3DPS::
    DESTINATION "${PROJECT_NAME}/lib/cmake/${PROJECT_NAME}"
)

configure_package_config_file(
    ${CMAKE_SOURCE_DIR}/cmake/${PROJECT_NAME}Config.cmake.in       # <input>
    ${PROJECT_NAME}Config.cmake                     # <output>
    INSTALL_DESTINATION "${PROJECT_NAME}/lib/cmake/${PROJECT_NAME}"
)

if(NOT BUILD_SHARED_LIBS)
    file(APPEND
        ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake 
        "add_definitions(-DIMPORT_STATIC_JX3DPS_LIB)\n"
    )
endif(NOT BUILD_SHARED_LIBS)

install(FILES 
    ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake
    ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake
    DESTINATION "${PROJECT_NAME}/lib/cmake/${PROJECT_NAME}"
)
